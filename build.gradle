buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
    }
}

apply plugin: "forge"
apply plugin: "curseforge"

ext.configFile = file "build.properties"
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

ext.secretFile = file "secret.properties"
secretFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.secret = new ConfigSlurper().parse prop
}

import groovy.json.JsonSlurper
ext.versionFile = file "versions.json"
project.ext.versions = new JsonSlurper().parseText versionFile.text

version = config.mod_version + "-" + config.minecraft_version
group = config.mod_group
archivesBaseName = config.mod_name
sourceCompatibility = targetCompatibility = 1.6

minecraft {
    version = config.minecraft_version + "-" + config.forge_version + "-" + config.minecraft_version
    runDir = "minecraft"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version":project.version, "mcversion":project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    manifest {
        attributes "FMLAT" : "llibrary_at.cfg"
    }
}

task devJar(type: Jar) {
    classifier = "dev"
    from sourceSets.main.output
    manifest {
        attributes "FMLAT" : "llibrary_at.cfg"
    }
}

task srcJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
	manifest {
        attributes "FMLAT" : "llibrary_at.cfg"
    }
}

artifacts {
    archives devJar, srcJar
}

curse {
    projectId = "230798"
    apiKey = secret.curseForgeApiKey
    releaseType = "release"
    changelog = versions.versions[versions.newestVersion].join("\n")

    displayName = "LLibrary " + config.mod_version + " for Minecraft " + config.minecraft_version

    additionalArtifact devJar, srcJar
}