buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "0.3.0-1.7.10"
group = "net.ilexiconn.llibrary"
archivesBaseName = "LLibrary"

minecraft {
    version = "1.7.10-10.13.3.1401-1710ls"
    runDir = "minecraft"
    replace '${version}', project.version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLAT' : 'llibrary_at.cfg'
    }
}

task devJar(type: Jar) {
    classifier = 'dev'
    from sourceSets.main.output
    manifest {
        attributes 'FMLAT' : 'llibrary-dev_at.cfg'
    }
}

task srcJar(type: Jar, dependsOn: classes) {
    classifier = 'src'
    from sourceSets.main.allSource
	manifest {
        attributes 'FMLAT' : 'llibrary-dev_at.cfg'
    }
}

task docJar(type: Jar, dependsOn: javadoc) {
    classifier = 'doc'
    from javadoc.destinationDir
}

artifacts {
    archives devJar, srcJar, docJar
}